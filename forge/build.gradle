buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'gradle.plugin.com.matthewprenger', name: 'CurseGradle', version: '1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'

version = "${mod_version}"
group = "top.theillusivec4.${mod_id}"
archivesBaseName = "${mod_id}-forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
    mappings channel: 'official', version: "${version_minecraft}"
	
    runs {
        client {
            workingDirectory project.file('run')
            
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                cakechomps {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                cakechomps {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_forge}"
}

sourceSets {
    main {
        resources {
            srcDirs += '../docs'
        }
    }
}

jar {
    from('CHANGELOG.md')

    manifest {
        attributes(["Specification-Title": "${mod_id}",
                    "Specification-Vendor": "${mod_author}",
                    "Specification-Version": "1",
                    "Implementation-Title": "${mod_id}",
                    "Implementation-Vendor" :"${mod_author}",
                    "Implementation-Version": "${mod_version}",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
}

jar.finalizedBy('reobfJar')

curseforge {
    apiKey = findProperty('curseKey') ?: 0
    project {
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion '1.17.1'
        addArtifact(sourcesJar)
    }
}
